// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        //https://developer.android.com/jetpack/androidx/releases/compose-compiler#declaring_dependencies
        compose_version = '1.0.5'
        gradle_version = '1.5.31'
        accompanist_version = '0.18.0'
        junit_version = '4.13.2'
        detekt_version = '1.19.0'
        dokka_version = "1.6.0"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$gradle_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"

        // Dependencies belong in the individual module build.gradle files
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "$detekt_version"
    id "org.jetbrains.dokka" version "$dokka_version"
}

tasks.named("detekt").configure {
    reports {
        xml {
            required = true
            outputLocation = file("build/reports/detekt/detekt.xml")
        }
        html {
            required = true
            outputLocation = file("build/reports/detekt/detekt.html")
        }
        txt.required.set(false)
        sarif.required.set(false)
    }
}

subprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'
    detekt {
        // Version of Detekt that will be used. When unspecified the latest detekt
        // version found will be used. Override to stay on the same version.
        toolVersion = "$detekt_version"

        // Builds the AST in parallel. Rules are always executed in parallel.
        // Can lead to speedups in larger projects. `false` by default.
        parallel = false

        // Define the detekt configuration(s) you want to use.
        // Defaults to the default detekt configuration.
        config = files("$rootDir/config/detekt/detekt.yml")

        // Turns on all the rules. `false` by default.
        allRules = false

        // Adds debug output during task execution. `false` by default.
        debug = false

        // If set to `true` the build does not fail when the
        // maxIssues count was reached. Defaults to `false`.
        ignoreFailures = true

        // Android: Don't create tasks for the specified build types (e.g. "release")
//        ignoredBuildTypes = ["release"]

        // Android: Don't create tasks for the specified build flavor (e.g. "production")
//        ignoredFlavors = ["production"]

        // Android: Don't create tasks for the specified build variants (e.g. "productionRelease")
//        ignoredVariants = ["productionRelease"]

        // Specify the base path for file paths in the formatted reports.
        // If not set, all file paths reported will be absolute file path.
        basePath = projectDir
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
