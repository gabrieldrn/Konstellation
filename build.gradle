// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
}

plugins {
    alias libs.plugins.android.application apply false
    alias libs.plugins.android.library apply false
    alias libs.plugins.kotlin.android apply false
    alias libs.plugins.kotlin.kapt apply false
    alias libs.plugins.versions
    alias libs.plugins.versionsCatalogUpdate
    alias libs.plugins.detekt
    alias libs.plugins.dokka apply false //Globally
}

tasks.named("detekt").configure {
    reports {
        xml {
            required = true
            outputLocation = file("build/reports/detekt/detekt.xml")
        }
        html {
            required = true
            outputLocation = file("build/reports/detekt/detekt.html")
        }
        txt.required.set(false)
        sarif.required.set(false)
    }
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

versionCatalogUpdate {
    sortByKey = false
}

subprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt' //Is there a way to use version catalog?
    detekt {
        // Version of Detekt that will be used. When unspecified the latest detekt
        // version found will be used. Override to stay on the same version.
        toolVersion = libs.versions.detekt.get()

        // Builds the AST in parallel. Rules are always executed in parallel.
        // Can lead to speedups in larger projects. `false` by default.
        parallel = false

        // Define the detekt configuration(s) you want to use.
        // Defaults to the default detekt configuration.
        config = files("$rootDir/config/detekt/detekt.yml")

        // Turns on all the rules. `false` by default.
        allRules = false

        // Adds debug output during task execution. `false` by default.
        debug = false

        // If set to `true` the build does not fail when the
        // maxIssues count was reached. Defaults to `false`.
        ignoreFailures = true

        // Android: Don't create tasks for the specified build types (e.g. "release")
//        ignoredBuildTypes = ["release"]

        // Android: Don't create tasks for the specified build flavor (e.g. "production")
//        ignoredFlavors = ["production"]

        // Android: Don't create tasks for the specified build variants (e.g. "productionRelease")
//        ignoredVariants = ["productionRelease"]

        // Specify the base path for file paths in the formatted reports.
        // If not set, all file paths reported will be absolute file path.
        basePath = projectDir
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
